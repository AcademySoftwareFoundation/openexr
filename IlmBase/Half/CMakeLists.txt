# yue.nicholas@gmail.com

ADD_EXECUTABLE ( eLut eLut.cpp )
ADD_CUSTOM_COMMAND(
  OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/eLut.h
  COMMAND eLut ARGS > ${CMAKE_CURRENT_BINARY_DIR}/eLut.h
  DEPENDS eLut
  WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
  )
SET_SOURCE_FILES_PROPERTIES(
  ${CMAKE_CURRENT_BINARY_DIR}/eLut.h
  PROPERTIES HEADER_FILE_ONLY TRUE
  )

ADD_EXECUTABLE ( toFloat toFloat.cpp )
ADD_CUSTOM_COMMAND(
  OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/toFloat.h
  COMMAND toFloat ARGS > ${CMAKE_CURRENT_BINARY_DIR}/toFloat.h
  DEPENDS toFloat
  WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
  )
SET_SOURCE_FILES_PROPERTIES(
  ${CMAKE_CURRENT_BINARY_DIR}/toFloat.h
  PROPERTIES HEADER_FILE_ONLY TRUE
  )

SET_SOURCE_FILES_PROPERTIES(
  half.cpp
  PROPERTIES
  OBJECT_DEPENDS
  "${CMAKE_CURRENT_BINARY_DIR}/eLut.h;${CMAKE_CURRENT_BINARY_DIR}/toFloat.h"
  )

SET (ILMBASE_LIB_TARGETS "")

IF (BUILD_SHARED_LIBS)
  LIST ( APPEND ILMBASE_LIB_TARGETS Half )

  ADD_LIBRARY ( Half SHARED
    half.cpp
    )

  TARGET_COMPILE_DEFINITIONS ( Half PRIVATE HALF_EXPORTS )
  IF (WIN32)
    TARGET_COMPILE_DEFINITIONS ( Half PUBLIC OPENEXR_DLL )
  ENDIF ()

  SET_TARGET_PROPERTIES ( Half
    PROPERTIES
    VERSION ${ILMBASE_VERSION_MAJOR}.${ILMBASE_VERSION_MINOR}.${ILMBASE_VERSION_PATCH}
    SOVERSION ${ILMBASE_VERSION_MAJOR}
    OUTPUT_NAME "Half${ILMBASE_LIBSUFFIX}"
    )

  ADD_DEPENDENCIES ( Half toFloat eLut )
ENDIF ()

IF (BUILD_STATIC_LIBS)
  LIST ( APPEND ILMBASE_LIB_TARGETS Half_static )

  ADD_LIBRARY ( Half_static STATIC
    half.cpp
    )

  SET_TARGET_PROPERTIES ( Half_static
    PROPERTIES
    VERSION ${ILMBASE_VERSION_MAJOR}.${ILMBASE_VERSION_MINOR}.${ILMBASE_VERSION_PATCH}
    OUTPUT_NAME "Half${ILMBASE_LIBSUFFIX}_s"
    )

  ADD_DEPENDENCIES ( Half_static toFloat eLut )
ENDIF ()

IF (BUILD_SHARED_LIBS OR BUILD_STATIC_LIBS)
  INSTALL ( TARGETS
    ${ILMBASE_LIB_TARGETS}
      ARCHIVE DESTINATION lib
      LIBRARY DESTINATION lib
      RUNTIME DESTINATION ${RUNTIME_DIR}
  )
ENDIF ()

INSTALL ( 
  FILES 
    half.h
    halfFunction.h
    halfExport.h
    halfLimits.h

  DESTINATION
    include/OpenEXR
)
